
;GEOS joystick input driver
;reassembled by Maciej 'YTM/Alliance' Witkowiak
;18.04.1999

;This is the source code for generic joystick driver

		;*=MOUSE_JMP
						;
MouseJMP	JMP _MouseInit			;fe80
SlowMouse	JMP _SlowMouse			;fe83
UpdateMouse	JMP _UpdateMouse		;fe86
SetMouse					;fe89

joyStat0	.byte 0				;fe89
joyStat1	.byte 0				;fe8a
joyStat2	.byte 0				;fe8b
joyStat3	.byte 0				;fe8c
joyStat4	.byte 0				;fe8d
joyStat5	.byte 0				;fe8e
joyStat6	.byte 0				;fe8f
joyStat7	.byte 0				;fe90

_MouseInit					;fe91
		JSR _SlowMouse
		STA joyStat2
		STA mouseXPos
		STA mouseXPos+1
		STA mouseYPos
		#LoadB inputData, $ff
		JMP JProc1_4

_SlowMouse					;fea5
		#LoadB mouseSpeed, NULL
SlowMse0	RTS

_UpdateMouse					;feab
		JSR JoyProc3
		#bbrf MOUSEON_BIT, mouseOn, SlowMse0
		JSR JoyProc1
		JSR JoyProc2
		LDY #0
		LDA joyStat4
		BPL UpdMse0
		DEY
UpdMse0		STY r1H				;fec0
		ASL A
		ROL r1H
		ASL A
		ROL r1H
		ASL A
		ROL r1H
		#add joyStat1
		STA joyStat1
		LDA r1H
		ADC mouseYPos
		STA mouseYPos
		RTS

JoyProc1	LDX inputData			;fed9
		BMI JProc1_2
		#CmpB maxMouseSpeed, mouseSpeed
		BCC JProc1_1
		#AddB mouseAccel, joyStat2
		BCC JProc1_4
		INC mouseSpeed
		#bra JProc1_4
JProc1_1	STA mouseSpeed			;fef8
JProc1_2	#CmpB minMouseSpeed, mouseSpeed	;fefb
		BCS JProc1_3
		#SubB mouseAccel, joyStat2
		BCS JProc1_4
		DEC mouseSpeed
		#bra JProc1_4
JProc1_3	STA mouseSpeed			;ff15
JProc1_4	LDX inputData			;ff18
		BMI JProc1_5
		LDY mouseSpeed
		STY r0L
		JSR JoyProc4
		#MoveB r1H, joyStat3
		#MoveB r2H, joyStat4
		RTS
JProc1_5	#LoadB joyStat3, 0		;ff30
		STA joyStat4
		RTS

JoyProc2	LDY #$ff			;ff39
		LDA joyStat3
		BMI JProc2_1
		INY
JProc2_1	STY r11H			;ff41
		STY r12L
		ASL A
		ROL r11H
		ASL A
		ROL r11H
		ASL A
		ROL r11H
		#add joyStat0
		STA joyStat0
		LDA r11H
		ADC mouseXPos
		STA mouseXPos
		LDA r12L
		ADC mouseXPos+1
		STA mouseXPos+1
		RTS

JoyProc3	LDA #$ff			;ff62
		STA cia1base
		LDA cia1base+1
		EOR #$ff
		CMP joyStat7
		STA joyStat7
		BNE JProc3_2
		AND #%00001111
		CMP joyStat6
		BEQ JProc3_1
		STA joyStat6
		TAY
		LDA JoyDirectionTab,y
		STA inputData
		#smbf INPUT_BIT, pressFlag
		JSR JProc1_4
JProc3_1	LDA joyStat7			;ff8e
		AND #%00010000
		CMP joyStat5
		BEQ JProc3_2
		STA joyStat5
		ASL
		ASL
		ASL
		EOR #%10000000
		STA mouseData
		#smbf MOUSE_BIT, pressFlag
JProc3_2	RTS				;ffa9

JoyDirectionTab					;ffaa
		.byte $ff, $02, $06, $ff
		.byte $04, $03, $05, $ff
		.byte $00, $01, $07, $ff
		.byte $ff, $ff, $ff, $ff

JoyProc4	LDA JoyTab1,x			;ffba
		STA r1L
		LDA JoyTab1+2,x
		STA r2L
		LDA JoyTab2,x
		PHA
		LDX #r1
		LDY #r0
		JSR BBMult
		LDX #r2
		JSR BBMult
		PLA
		PHA
		BPL JProc4_1
		LDX #r1
		JSR Dnegate
JProc4_1	PLA				;ffdd
		AND #%01000000
		BEQ JProc4_2
		LDX #r2
		JSR Dnegate
JProc4_2	RTS				;ffe7

JoyTab1						;ffe8
		.byte $ff, $b5, $00, $b5
		.byte $ff, $b5, $00, $b5
		.byte $ff, $b5
JoyTab2						;fff2
		.byte $00, $40, $40, $c0
		.byte $80, $80, $00, $00

;fffa
		